buildscript {
    repositories {
        exclusiveContent {
            forRepository {
                maven {
                    name 'DFKI-MLT'
                    url 'https://raw.githubusercontent.com/DFKI-MLT/Maven-Repository/main'
                }
            }
            filter {
                includeGroup 'de.dfki.lt.jtok'
            }
        }
    }
}

plugins {
    id "de.dfki.mary.lexicon-compiler" version "0.3.0"
    id 'maven-publish'
    id 'signing'
}

group 'de.dfki.mary'
version '0.3.0-SNAPSHOT'
description 'Lower Sorbian lexicon for MaryTTS'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/marytts/marytts-lexicon-dsb'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 only'
                        url = 'https://spdx.org/licenses/LGPL-3.0-only.html'
                    }
                }
                developers {
                    developer {
                        id = 'psibre'
                        name = 'Ingmar Steiner'
                    }
                    developer {
                        id = 'aStereoID'
                        name = 'Astrid Schmiedel'
                    }
                    developer {
                        id = 'JMK-CB'
                        name = 'Jan Meschkank'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/marytts/marytts-lexicon-dsb.git'
                    developerConnection = 'scm:git:ssh://git@github.com:marytts/marytts-lexicon-dsb.git'
                    url = 'https://github.com/marytts/marytts-lexicon-dsb'
                }
            }
        }
    }
    repositories {
        maven {
            name 'OSSRH'
            url isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    : 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
